#Область ПрограммныйИнтерфейс

// НайтиПоИмениФайла осуществляет поиск внешних обработок по имени файла.
// 
// Параметры:
// 	ИмяФайла - Строка - полное имя файла (с расширением);
// 
// Возвращаемое значение:
//   - Массив из Структура - описание:
//		* Ссылка - СправочникСсылка.ВнешниеОбработки - элемент справочника;
//		* НомерСтроки - Число - номер строки табличной части, если файл найден
//								в табличной части Принадлежность, иначе - 0;
//
Функция НайтиПоИмениФайла( Знач ИмяФайла ) Экспорт
	
	Перем Запрос;
	Перем РезультатЗапроса;
	Перем Выборка;
	Перем АдресХраненияФайла;
	Перем Результат;
	
	Результат = Новый Массив();
	
	Если ( ПустаяСтрока(ИмяФайла) ) Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр( "СтрокаПоиска", ИмяФайла );
	Запрос.Текст =	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	|	ВнешниеОбработки.Ссылка КАК Ссылка,
	              	|	0 КАК НомерСтроки
	              	|ИЗ
	              	|	Справочник.ВнешниеОбработки КАК ВнешниеОбработки
	              	|ГДЕ
	              	|	ВнешниеОбработки.ИмяФайла = &СтрокаПоиска
	              	|
	              	|ОБЪЕДИНИТЬ ВСЕ
	              	|
	              	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	|	ВнешниеОбработкиПринадлежность.Ссылка,
	              	|	ВнешниеОбработкиПринадлежность.НомерСтроки
	              	|ИЗ
	              	|	Справочник.ВнешниеОбработки.Принадлежность КАК ВнешниеОбработкиПринадлежность
	              	|ГДЕ
	              	|	(ВЫРАЗИТЬ(ВнешниеОбработкиПринадлежность.ИмяФайлаПечатнойФормы КАК СТРОКА(260))) = &СтрокаПоиска";

	РезультатЗапроса = Запрос.Выполнить();
	
	Если ( РезультатЗапроса.Пустой() ) Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		
		АдресХраненияФайла = АдресХраненияФайла();
		ЗаполнитьЗначенияСвойств( АдресХраненияФайла, Выборка );
		Результат.Добавить( АдресХраненияФайла );
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// СохранитьВнешнююОбработку сохраняет в информационную базу файл внешней обработки с заполнением комментария к нему.
// 
// Параметры:
// 	АдресХраненияФайла - Структура - описание:
//		* Ссылка - СправочникСсылка.ВнешниеОбработки - элемент справочника;
//		* НомерСтроки - Число - номер строки табличной части или 0,
//								если обработка сохраняется в самом элементе справочника;
// 	ИмяФайла - Строка - имя файла внешней обработки;
// 	Файл - Файл - дескриптор загружаемого файла;
// 	Данные - ДвоичныеДанные - содержимое файла;
// 
Процедура СохранитьВнешнююОбработку( Знач АдресХраненияФайла, Знач ИмяФайла, Знач Файл, Знач Данные ) Экспорт
	
	Перем ЭлементСправочника;
	Перем Комментарий;
	Перем НовоеХранилищеЗначения;
	
	ЭлементСправочника = АдресХраненияФайла.Ссылка.ПолучитьОбъект();
	
	Комментарий = НСтр( "ru = ' загружено из внешнего хранилища.';
						|en = ' loaded from external storage.'" );
							
	Комментарий = ВнешниеОтчетыИОбработкиКлиентСервер.КомментарийКФайлу( Файл, ТекущаяДатаСеанса(), Комментарий );
	
	НовоеХранилищеЗначения = Новый ХранилищеЗначения( Данные );
	
	ЭлементСправочника.Заблокировать();
	
	Если ( ЗначениеЗаполнено(АдресХраненияФайла.НомерСтроки) ) Тогда
		
		Индекс = АдресХраненияФайла.НомерСтроки - 1;
		ЭлементСправочника.Принадлежность[Индекс].ИмяФайлаПечатнойФормы = ИмяФайла;
		ЭлементСправочника.Принадлежность[Индекс].КомментарийКФайлуИсточнику = Комментарий;
		ЭлементСправочника.Принадлежность[Индекс].ХранилищеВнешнейОбработки = НовоеХранилищеЗначения;
	
	Иначе
		
		ЭлементСправочника.ИмяФайла = ИмяФайла;
		ЭлементСправочника.КомментарийКФайлуИсточнику = Комментарий;
		ЭлементСправочника.ХранилищеВнешнейОбработки = НовоеХранилищеЗначения;
		
	КонецЕсли;

	ЭлементСправочника.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// АдресХраненияФайла описывает где хранится файл внешней обработки.
// 
// Возвращаемое значение:
// 	- Структура - описание:
//		* Ссылка - СправочникСсылка.ВнешниеОбработки - элемент справочника;
//		* НомерСтроки - Число - номер строки табличной части или 0,
//								если обработка хранится в самом элементе справочника);
// 
Функция АдресХраненияФайла()
	
	Перем Результат;
	
	Результат = Новый Структура();
	Результат.Вставить( "Ссылка" );
	Результат.Вставить( "НомерСтроки" );
	
	Возврат Результат;
	   
КонецФункции

#КонецОбласти