#Область СлужебныйПрограммныйИнтерфейс

Функция VersionGET( Запрос )
	
	Перем Ответ;
	
	Ответ = Новый HTTPСервисОтвет( КодОтветаHTTP.КодыОтветаHTTP().OK );
	Ответ.УстановитьТелоИзСтроки( ВнешниеОтчетыИОбработкиСервер.JSONСхемаОтвета( ВерсияКонфигурации() ) );
	
	Возврат Ответ;
	
КонецФункции

Функция UpdatePOST( Запрос )
	
	Перем ИмяФайла;
	Перем Данные;
	Перем Результат;
	Перем Ответ;

	Ответ = Новый HTTPСервисОтвет( КодОтветаHTTP.КодыОтветаHTTP().OK );
	
	ВнешниеОтчетыИОбработкиСервер.ПроверитьЗапросUpdate( Запрос, Ответ );

	Если ( КодОтветаHTTP.isOk(Ответ.КодСостояния) ) Тогда
		
		ИмяФайла = РаскодироватьСтроку( Запрос.Заголовки.Получить("Name"), СпособКодированияСтроки.КодировкаURL );
		Данные = Запрос.ПолучитьТелоКакДвоичныеДанные();
		Результат = ВнешниеОтчетыИОбработкиСервер.РезультатОбновленияИнформационнойБазы( ИмяФайла, Данные );
		СформироватьОтветJSON( Ответ, Результат );
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция SwitchPOST( Запрос )
	
	Перем Value;
	Перем Результат;
	Перем Ответ;

	Ответ = Новый HTTPСервисОтвет( КодОтветаHTTP.КодыОтветаHTTP().OK );
	
	ВнешниеОтчетыИОбработкиСервер.ПроверитьЗапросSwitch( Запрос, Ответ );
	
	Если ( КодОтветаHTTP.isOk(Ответ.КодСостояния) ) Тогда
		
		Value = Запрос.Заголовки.Получить("Value");
		Value = ?(Value = "true", Истина, Ложь);
		Результат = ВнешниеОтчетыИОбработкиСервер.РезультатПереключенияФункциональнойОпцииЗагрузкиФайлов( Value );
		СформироватьОтветJSON( Ответ, Результат );
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьОтветJSON( Ответ, Знач Результат )
	
	Ответ.Заголовки.Вставить( "Content-Type", "application/json" );
	Ответ.УстановитьТелоИзСтроки( ВнешниеОтчетыИОбработкиСервер.JSONСхемаОтвета( Результат ) );
	
КонецПроцедуры

Функция ВерсияКонфигурации()
	
	Перем Результат;
	
	Результат = ВнешниеОтчетыИОбработкиСервер.СтруктураОтвета();
	Результат.Тип = "info";
	Результат.Сообщение = ОбщегоНазначения.МетаданныеВерсия();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

